{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                              Official SCAR Include
                                 Timing Routines
--------------------------------------------------------------------------------
 * procedure WaitRR(Min, Max);
 * function WaitFunc(Func: function: Boolean; DesiredResult: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
 * function WaitFuncEx(Func: string; Parameters: TVariantArray; DesiredResult: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
 * function WaitFindColorTol(var X, Y: Integer; Color, X1, Y1, X2, Y2, Tol: Integer; DesiredResult: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
 * function WaitFindColorTolEx(var TPA: TPointArray; Color, X1, Y1, X2, Y2, Tol, DesiredCount: Integer; GreaterThan: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
 * procedure WaitNone;
 * function TheTime: string;
 * function TheDate: string;
 * function TheMonth: string;
 * function STimeRunning: string;
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
procedure WaitRR(Min, Max: LongInt);
Contributors: Wanted, Freddy
Description: Waits/sleeps a random amount of time.
Date Created: August 10th, 2011. By Wanted
Last Modified: July 20th, 2012. By Freddy
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

procedure WaitRR(Min, Max: LongInt);
begin
  Wait(RR(Min, Max));
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function WaitFunc(Func: function: Boolean; DesiredResult: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
Contributors: Wanted
Description: Repeats a function until DesiredResult.
Date Created: August 10th, 2011. By Wanted
Last Modified: September 26th, 2011. By Wanted
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function WaitFunc(Func: function: Boolean; DesiredResult: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
var
  T: LongInt;
begin
  Result := True;
  T := GetSystemTime + RR(MinTotalWait, MaxTotalWait);
  while (GetSystemTime < T) do
    if (Func() = DesiredResult) then
      Exit
    else
      WaitRR(MinWait, MaxWait);
  Result := False;
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function WaitFuncEx(Func: string; Parameters: TVariantArray; DesiredResult: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
Contributors: Wanted
Description: Repeats a function until DesiredResult.
Date Created: September 26th, 2011. By Wanted
Last Modified: September 26th, 2011. By Wanted
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function WaitFuncEx(Func: string; Parameters: TVariantArray; DesiredResult: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
var
  T: LongInt;
begin
  Result := True;
  T := GetSystemTime + RR(MinTotalWait, MaxTotalWait);
  while (GetSystemTime < T) do
    if (CallProc(Func, Parameters) = DesiredResult) then
      Exit
    else
      WaitRR(MinWait, MaxWait);
  Result := False;
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function WaitFindColorTol(var X, Y: Integer; Color, X1, Y1, X2, Y2, Tol: Integer; DesiredResult: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
Contributors: Wanted, Freddy
Description: Repeats FindColorTol until DesiredResult.
Date Created: September 26th, 2011. By Wanted
Last Modified: July 20th, 2012. By Freddy
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function WaitFindColorTol(var X, Y: Integer; Color, X1, Y1, X2, Y2, Tol: Integer; DesiredResult: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
var
  T: LongInt;
begin
  Result := True;
  T := GetSystemTime + RR(MinTotalWait, MaxTotalWait);
  while (GetSystemTime < T) do
    if (FindColorTol(X, Y, Color, X1, Y1, X2, Y2, Tol) = DesiredResult) then
      Exit
    else
      WaitRR(MinWait, MaxWait);
  Result := False;
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function WaitFindColorTolEx(var TPA: TPointArray; Color, X1, Y1, X2, Y2, Tol, DesiredCount: Integer; GreaterThan: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
Contributors: Wanted, Freddy
Description: Repeats FindColorTolEx until greater or les than  Count.
Date Created: October 28th, 2011. By Wanted
Last Modified: July 20th, 2012. By Freddy
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function WaitFindColorTolEx(var TPA: TPointArray; Color, X1, Y1, X2, Y2, Tol, DesiredCount: Integer; GreaterThan: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
var
  T, L: LongInt;
begin
  Result := True;
  T := GetSystemTime + RR(MinTotalWait, MaxTotalWait);
  while (GetSystemTime < T) do
  begin
    FindColorTolEx(TPA, Color, X1, Y1, X2, Y2, Tol);
    L := Length(TPA);
    if (GreaterThan) then
    begin
      if (L > DesiredCount) then
        Exit;
    end
    else
      if (L < DesiredCount) then
        Exit;
    WaitRR(MinWait, MaxWait);
  end;
  Result := False;
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
procedure WaitNone;
Contributors: Wanted
Description: For use with while (this) do DoNothing
Date Created: August 10th, 2011. By Wanted
Last Modified: August 10th, 2011. By Wanted
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

procedure WaitNone;
begin
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function TheTime: string;
By: Timer
Description: Gets the Time.
Date Created: August 7th, 2011. By Timer
Last Modified: August 7th, 2011. By Timer
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function TheTime: string;
var
  Hour, Minz, Sec, MSec: Word;
  Padding: string;
begin
  DecodeTime(Now, Hour, Minz, Sec, MSec);
  Padding := 'AM';
  if (Hour > 12) then
  begin
    Hour := Hour - 12;
    Padding := 'PM';
  end else
    if (Hour = 12) then
      Padding := 'PM';
  Result := IntToStr(Hour) + ':' + IntToStr(Minz) + ':' + IntToStr(Sec) + ' ' + Padding;
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function TheDate: string;
By: Timer, Freddy
Description: Gets the Date.
Date Created: August 7th, 2011. By Timer
Last Modified: July 20th, 2012. By Freddy
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function TheDate: string;
var
  Year, Month, Day: Word;
  Months, suffixs: TStrArray;
  suffix: string;
begin
  DecodeDate(Date, Year, Month, Day);
  Months := ['January', 'February', 'March', 'April', 'May', 'June', 'July',
            'August', 'September', 'October', 'November', 'December'];
  suffixs := ['st', 'nd', 'rd'];
  if (InRange(Day mod 10, 1, 3)) and (not InRange(Day, 11, 13)) then
    suffix := suffixs[(Day mod 10) - 1]
  else
    suffix := 'th';
  Result := Months[Month - 1] + ' ' + IntToStr(Day) + suffix + ', ' + IntToStr(Year);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function TheMonth: string;
By: Timer, Freddy
Description: Gets the Month.
Date Created: August 7th, 2011. By Timer
Last Modified: July 20th, 2012. By Freddy
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function TheMonth: string;
var
  Year, Month, Day: Word;
  Months: TStrArray;
begin
  DecodeDate(Date, Year, Month, Day);
  Months := ['January', 'February', 'March', 'April', 'May', 'June', 'July',
            'August', 'September', 'October', 'November', 'December'];
  Result := Months[Month - 1];
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function STimeRunning: string;
By: Timer, Freddy, LordJashin
Description: Returns time running.
Date Created: August 7th, 2011. By Timer
Last Modified: October 3rd, 2012. By LordJashin
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function STimeRunning: string;
var
  tr: Integer;
  tD, tH, tM, tS: Integer;
  TheTimeRInfo: array[0..3] of TStrArray;
  tDD, tHH, tMM, tSS: Boolean;
begin
  tr := GetTimeRunning;      
  if (tr < 1000) then
    begin
      Result := IntToStr(tr) + ' milliseconds';
      Exit;
    end;
  ConvertTime(tr, tH, tM, tS);
  TheTimeRInfo[0] := [' day, ', ' days, ', ' day ', ' days '];
  TheTimeRInfo[1] := [' hour, ', ' hours, ', ' hour ', ' hours'];
  TheTimeRInfo[2] := [' minute, ', ' minutes, ', ' minute', ' minutes '];
  TheTimeRInfo[3] := [' second', ' seconds'];
  tD := tH div 24;
  tDD := tD > 0;
  tHH := tH > 0;
  tMM := tM > 0;
  tSS := tS > 0;
  if (tDD) then
    tH := tH - (tD * 24);
  if (tDD) then
    if (tD > 1) then
      if (tHH) or (tMM) or (tSS) then
        Result := Result + IntToStr(tD) + TheTimeRInfo[0][1]
      else
        Result := Result + IntToStr(tD) + TheTimeRInfo[0][3]
    else
      if (tHH) or (tMM) or (tSS) then
        Result := Result + IntToStr(tD) + TheTimeRInfo[0][0]
      else
        Result := Result + IntToStr(tD) + TheTimeRInfo[0][2];
  if (tHH) then
    if (tH > 1) then
      if (tMM) or (tSS) then
        Result := Result + IntToStr(tH) + TheTimeRInfo[1][1]
      else
        Result := Result + IntToStr(tH) + TheTimeRInfo[1][3]
    else
      if (tMM) or (tSS) then
        Result := Result + IntToStr(tH) + TheTimeRInfo[1][0]
      else
        Result := Result + IntToStr(tH) + TheTimeRInfo[1][2];
  if (tMM) then
    if (tM > 1) then
      if (tSS) then
        Result := Result + IntToStr(tM) + TheTimeRInfo[2][1]
      else
        Result := Result + IntToStr(tM) + TheTimeRInfo[2][3]
    else
      if (tSS) then
        Result := Result + IntToStr(tM) + TheTimeRInfo[2][0]
      else
        Result := Result + IntToStr(tM) + TheTimeRInfo[2][2];
  if (tSS) then
    if (tS > 1) then
      Result := Result + IntToStr(tS) + TheTimeRInfo[3][1]
    else
      Result := Result + IntToStr(tS) + TheTimeRInfo[3][0];
end;

//MSToTime