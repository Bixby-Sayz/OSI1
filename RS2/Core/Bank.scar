{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                              Official SCAR Include
                                   Runescape 2
                                  Bank routines
--------------------------------------------------------------------------------
 * function BankScreen: Boolean;
     By: Wanted
 * function PinScreen: Boolean;
     By: Wanted
 * function EnterBankPin(Pin: string): Boolean;
     By: Wanted
 * function CloseBank(CheckBS: Boolean): Boolean;
     By: Wanted
 * function DepositAll(CheckBS, Inven, Equip, Summon: Boolean): Boolean;
     By: Wanted
 * function ToggleUseNotes(Use: Boolean): Boolean;
     By: Wanted
 * function SearchBank(CheckBS: Boolean; SearchText: string): Boolean;
     By: Wanted
 * function FixBank(FixUp: Boolean): Boolean;
     By: Wanted
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function BankScreen: Boolean;
Contributors: Wanted
Description: Returns true if at the bank screen.
Date Created:  October 30th, 2011. By Wanted. RS2 Build 671.
Last Modification: October 30th, 2011. By Wanted. RS2 Build 671.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function BankScreen: Boolean;
var
  TPA: TPointArray;
  P: TPoint;
begin
  FindColors(TPA, 4106994, 119, 25, 328, 47);
  if (Length(TPA) > 20) then
    Result := FindTextTPAInTPA('ank of', UpChars, TPA, P.X, P.Y);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function PinScreen: Boolean;
Contributors: Wanted
Description: Returns true if at the pin screen.
Date Created:  November 8th, 2011. By Wanted. RS2 Build 676.
Last Modification: November 8th, 2011. By Wanted. RS2 Build 676.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function PinScreen: Boolean;
var
  TPA: TPointArray;
  P: TPoint;
begin
  FindColors(TPA, 2070783, 50, 57, 421, 79);
  if (Length(TPA) > 50) then
     Result := FindTextTPAInTPA('enter your', UpChars, TPA, P.X, P.Y);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function EnterBankPin(Pin: string): Boolean;
Contributors: Wanted
Description: Enters bank pin and returns true if succesfully entered pin and at bank screen.
Date Created:  November 8th, 2011. By Wanted. RS2 Build 676.
Last Modification: November 8th, 2011. By Wanted. RS2 Build 676.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function EnterBankPin(Pin: string): Boolean;
var
  I, II, C, T: Integer;
  TPA: TPointArray;
  TBA: TBoxArray;
  P: TPoint;
begin
  if (Length(Pin) <> 4) then
    Exit;
  if (not (PinScreen)) then
    Exit;
  SetLength(TBA, 11);
  for I := 0 to 10 do
    TBA[I] := Box(37 + ((I mod 4) * 90), 107 + ((I div 4) * 72), 100 + ((I mod 4) * 90), 170 + ((I div 4) * 72));
  for I := 0 to 3 do
  begin
    FindColors(TPA, 2070783, 37, 107, 370, 314);
    if (Length(TPA) < 250) then
      Exit;
    if (not (FindTextTPAInTPA(Copy(Pin, I + 1, 1), UpChars, TPA, P.X, P.Y))) then
      GetMousePos(P.X, P.Y);
    for II := 0 to 10 do
      if (PointInBox(P, TBA[II])) then
        Break;
    if (II > 10) then
      Exit;
    C := CountColor(clWhite, 41, 79, 399, 105);
    T := GetSystemTime + RR(5000, 7000);
    MouseBox(TBA[II].X1, TBA[II].Y1, TBA[II].X2, TBA[II].Y2, ClickLeft);
    while ((CountColor(clWhite, 41, 79, 399, 105) = C) and (GetSystemTime < T)) do
      WaitRR(250, 400);
    WaitRR(250, 400);
  end;
  Result := WaitFunc(@BankScreen, True, 250, 400, 5000, 7000);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function CloseBank(CheckBS: Boolean): Boolean;
Contributors: Wanted
Description: Returns true if at the bank screen and then closes it if open.
Date Created:  October 30th, 2011. By Wanted. RS2 Build 671.
Last Modification: October 30th, 2011. By Wanted. RS2 Build 671.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function CloseBank(CheckBS: Boolean): Boolean;
begin
  if (CheckBS) then
    if (not (BankScreen)) then
      Exit
    else
      Result := True;
  MouseBox(482, 28, 495, 41, ClickLeft);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function DepositAll(CheckBS, Inven, Equip, Summon: Boolean): Boolean;
Contributors: Wanted
Description: Returns true if at the bank screen and then clicks deposit all.
Date Created:  October 30th, 2011. By Wanted. RS2 Build 671.
Last Modification: October 30th, 2011. By Wanted. RS2 Build 671.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function DepositAll(CheckBS, Inven, Equip, Summon: Boolean): Boolean;
var
  TBoA: TBooleanArray;
  I: Integer;
begin
  if (CheckBS) then
    if (not (BankScreen)) then
      Exit
    else
      Result := True;
  TBoA := [Inven, Equip, Summon];
  for I := 0 to 2 do
    if (TBoA[I]) then
      MouseBox(379 + (38 * I), 298, 413 + (38 * I), 318, ClickLeft);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function ToggleUseNotes(Use: Boolean): Boolean;
Contributors: Wanted
Description: Enables or disables note usage. Returns true if toggle was needed.
Date Created:  October 30th, 2011. By Wanted. RS2 Build 671.
Last Modification: October 30th, 2011. By Wanted. RS2 Build 671.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function ToggleUseNotes(Use: Boolean): Boolean;
begin
  case GetColor(251, 301) of
    7967381: Result := not (Use);
    10862787: Result := Use;
  else
    Exit;
  end;
  if  (not (Result)) then
    Exit;
  MouseBox(244, 297, 275, 318, ClickLeft);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function SearchBank(CheckBS: Boolean; SearchText: string): Boolean;
Contributors: Wanted
Description: Returns true if at bankscreen and CheckBS.
Searches for SearchText using the in bank search glass.
Date Created:  October 30th, 2011. By Wanted. RS2 Build 671.
Last Modification: October 30th, 2011. By Wanted. RS2 Build 671.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function SearchBank(CheckBS: Boolean; SearchText: string): Boolean;
begin
  if (CheckBS) then
    if (not (BankScreen)) then
      Exit
    else
      Result := True;
  MouseBox(70, 297, 100, 319, ClickLeft);
  WaitRR(250, 500);
  TypeSend(SearchText);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function FixBank(FixUp: Boolean): Boolean;
Contributors: Wanted
Description: Scrolls bank all the way to top or bottom, true if not needed to do so.
Searches for SearchText using the in bank search glass.
Date Created:  October 30th, 2011. By Wanted. RS2 Build 671.
Last Modification: October 30th, 2011. By Wanted. RS2 Build 671.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function FixBank(FixUp: Boolean): Boolean;
var
  TPA: TPointArray;
  B1, B2, B3: TBox;
begin
  if (FixUp) then
    if (GetColor(489, 101) = 1250841) then
      Exit;
  if (not (FixUp)) then
    if (GetColor(489, 276) = 592137) then
      Exit;
  Result := True;
  if (not (FindColors(TPA, 1250841, 482, 101, 495, 276))) then
    Exit;
  B1 := TPABounds(TPA);
  if (not (FindColors(TPA, 592137, 482, 101, 495, 276))) then
    Exit;
  B2 := TPABounds(TPA);
  B3 := Box(482, B1.Y1, 495, B2.Y2);
  if (FixUp) then
    DragMouseBox(B3.X1, B3.Y1, B3.X2, B3.Y2, 478, 81, 502, 100, True)
  else
    DragMouseBox(B3.X1, B3.Y1, B3.X2, B3.Y2, 475, 279, 504, 300, True)
end;

//DepositScreen
//PinScreen
//InPin
//BankSlotToPoint
//PointToBankSlot
//BankSlotToBox
//DepositSlotToBox
//FixBankTab
//CurrentBankTab
//BankTabExists
//BankTab
//Deposit
//Withdraw
//OpenBank
//OpenDepositBox