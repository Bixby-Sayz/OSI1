{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                              Official SCAR Include
                                   Runescape 2
                                  Chat Routines
--------------------------------------------------------------------------------
 * function GetChatTabBounds(ChatTab: Integer): TBox;
 * function GetCurrentChat: Integer;
 * function GetChatStatus(Chat: Integer): Integer;
 * function SetChat(Chat, State: Integer): Boolean;
 * function SetChats(Chats, States: TIntArray): TBoolArray;
 * function FixChat: Boolean;
 * function GetChatLineBounds(Line: Integer): TBox;
 * function GetChatTextEx(Line, TextColor: Integer): string;
 * function LeveledUpEx(Click, UntilGone: Boolean): Boolean;
 * function LeveledUp: Boolean;
 * function ClickContinueEx(Click, UntilGone: Boolean): Boolean;
 * function ClickContinue: Boolean;
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function GetChatTabBounds(ChatTab: Integer): TBox;
Contributors: Wanted
Description: Returns the boundries of the chat tab you want.
Date Created: October 28th, 2011. By Wanted. RS2 Build 671.
Last Modified: January 9th, 2012. By Wanted. RS2 Build 695.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function GetChatTabBounds(ChatTab: Integer): TBox;
var
  Offset: Integer;
begin
  if ChatTab = 8 then Offset := MCTL;
  Result := IntToBox(MCT1 + (MCTS * ChatTab), MCT2, MCT3 + Offset + (MCTS * ChatTab), MCT4);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function GetCurrentChat: Integer;
Contributors: Wanted
Description: Returns the current chat you have open.
Date Created: October 28th, 2011. By Wanted. RS2 Build 671.
Last Modified: January 9th, 2012. By Wanted. RS2 Build 695.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function GetCurrentChat: Integer;
begin
  for Result := 0 to 7 do
    if (not (SimilarColors(5531509, GetColor(9 + (57 * Result), 484), 5))) then
      Break;
  if (Result > 7) then
    Result := -1;
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function GetChatStatus(Chat: Integer): Integer;
Contributors: Wanted, Freddy
Description: Returns the stats of the chat you enter.
Date Created: October 28th, 2011. By Wanted. RS2 Build 671.
Last Modified: July 20th, 2012. By Freddy. RS2 Build 725.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function GetChatStatus(Chat: Integer): Integer;
var
  Colors: TIntArray;
  P: TPoint;
  B: TBox;
begin
  Result := 0;
  if (Chat = 0) then
  begin
    Result := 0;
    Exit;
  end;
  Colors := [65280, 65535, 255, 16776960];
  B := GetChatTabBounds(Chat);
  for Result := 0 to 3 do
    if (FindColor(P.X, P.Y, Colors[Result], B.X1, B.Y1, B.X2, B.Y2)) then
      Exit;
  if (Result > 3) then
    Result := -1;
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function SetChat(Chat, State: Integer): Boolean;
Contributors: Wanted, Freddy
Description: Sets the desired chat to the desired state.
Date Created: October 28th, 2011. By Wanted. RS2 Build 671.
Last Modified: July 20th, 2012. By Freddy. RS2 Build 725.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function SetChat(Chat, State: Integer): Boolean;
var
  CurrentState, CurrentChat: Integer;
  Options: T2DStrArray;
  TypeC: ClickActions;
  B: TBox;
begin
  Result := True;
  CurrentChat := GetCurrentChat;
  CurrentState := GetChatStatus(Chat);
  if (CurrentChat = Chat) then
    if (State = Chat_View) then
      Exit;
  if (CurrentState = State) then
    Exit;
  Result := False;
  B := GetChatTabBounds(Chat);
  if (((State = 0) and (Chat = 0)) or (State = 5)) then
    TypeC := ClickLeft
  else
    TypeC := ClickRight;
  MouseBox(B.X1, B.Y1, B.X2, B.Y2, TypeC);
  if (TypeC = ClickLeft) then
  begin
    Result := True;
    Exit;
  end;
  Options := [TStrArray(['All', 'On']), TStrArray(['ilter', 'iend', 'Fri']), TStrArray(['Off', 'ff', 'Of']),
              TStrArray(['ide', 'Hi', 'id']), TStrArray(['XP', 'arned', 'ime'])];
  if (Chat = Chat_Friends_Tab) then
    Options[Chat_Friends] := ['ends F', 'nds Fr', 'ds Frie'];            
  if (InRange(State, 0, High(Options))) then
    Result := ChooseOptionMulti(Options[State]);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function SetChats(Chats, States: TIntArray): TBoolArray;
Contributors: Wanted, Freddy
Description: Sets the desired chats to the desired states.
Date Created: October 30th, 2011. By Wanted. RS2 Build 671.
Last Modified: July 20th, 2012. By Freddy. RS2 Build 725.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function SetChats(Chats, States: TIntArray): TBoolArray;
var
  I, H: Integer;
begin
  H := High(Chats);
  if (Length(Chats) > Length(States)) then
    H := Length(Chats) - (Length(Chats) - Length(States));
  SetLength(Result, H + 1);
  for I := 0 to H do
    Result[I] := SetChat(Chats[I], States[I]);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function FixChat: Boolean;
Contributors: Wanted, Freddy
Description: Scrolls the chat bar all the way down, if needed true.
Date Created: October 30th, 2011. By Wanted. RS2 Build 671.
Last Modified: July 20th, 2012. By Freddy. RS2 Build 725.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function FixChat: Boolean;
var
  TPA: TPointArray;
  B: TBox;
begin
  Result := True;
  if (GetColor(505, 441) = 592137) then
    Exit;
  Result := True;
  FindColorEx(TPA, 592137, 498, 361, 511, 440);
  if (Length(TPA) < 1) then
    Exit;
  B := TPABounds(TPA);
  DragMouseBox(B.X1, B.Y1, B.X2, B.Y2, 493, 442, 515, 461, True);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function GetChatLineBounds(Line: Integer): TBox;
Contributors: Wanted
Description: Returns the box around the chat line.
Date Created: November 5th, 2011. By Wanted. RS2 Build 675.
Last Modified: November 5th, 2011. By Wanted. RS2 Build 675.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function GetChatLineBounds(Line: Integer): TBox;
begin
  Result := Box(MCX1, MCY1 + (Line * 14), MCX2, ((MCY1 + 13) + (Line * 14)));
  if (Line = 8) then
  begin
    IncEx(Result.Y1, 2);
    IncEx(Result.Y2, 2);
  end;
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function GetChatTextEx(Line, TextColor: Integer): string;
Contributors: Wanted
Description: Returns chat text on a given line.
Date Created: November 5th, 2011. By Wanted. RS2 Build 675.
Last Modified: December 30th, 2011. By Wanted. RS2 Build 690.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function GetChatTextEx(Line, TextColor: Integer): string;
var
  B: TBox;
begin
  Result := '';
  if (FixChat) then
    WaitRR(500, 1000);
  B := GetChatLineBounds(Line);
  Result := GetTextAtEx(B.X1, B.Y1 - 1, 0, SmallChars, False, False, 0, 2, TextColor, 70, False, tr_AllChars);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function LeveledUpEx(Click, UntilGone: Boolean): Boolean;
Contributors: Wante, Freddy
Description: Clicks to continue after level up, true if found.
Date Created: November 7th, 2011. By Wanted. RS2 Build 676.
Last Modified: July 20th, 2012. By Freddy. RS2 Build 725.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function LeveledUpEx(Click, UntilGone: Boolean): Boolean;
var
  TPA, TPA2: TPointArray;
  C, T, I: Integer;
  TBA: TBoxArray;
  P, MP: TPoint;
begin
  Result := False;
  I := 0;
  repeat
    FindColorEx(TPA, clWhite, MCX1, 446, MCX2, MCY2);
    FindColorEx(TPA2, 16711680, MCX1, 446, MCX2, MCY2);
    TPA := CombineTPA(TPA, TPA2);
    if (Length(TPA) < 200) then
      Exit;
    SetLength(TBA, I + 1);
    TBA[I] := TPABounds(TPA);
    if (FindTextTPAInTPA('lick here to', NPCChars, TPA, P.X, P.Y)) then
    begin
      if (not (Result)) then
        Result := True;
    end
    else
      Exit;
    if (Click) then
    begin
      C := CountColor(clBlack, MCX1, MCY1, MCX2, 446);
      if (I < 1) then
      begin
        MouseBox(TBA[I].X1, TBA[I].Y1, TBA[I].X1, TBA[I].Y1, ClickLeft);
        GetMousePos(MP.X, MP.Y);
      end
      else
        if (TBA[I].X1 = TBA[I - 1].X1) then
          Mouse(MP.X, MP.Y, 0, 0, True)
        else
        begin
          MouseBox(TBA[I].X1, TBA[I].Y1, TBA[I].X1, TBA[I].Y1, ClickLeft);
          GetMousePos(MP.X, MP.Y);
        end;
      T := GetSystemTime + RR(10000, 15000);
      while ((CountColor(clBlack, MCX1, MCY1, MCX2, 446) = C) and (GetSystemTime < T)) do
        WaitRR(200, 400);
    end
    else
      Exit;
    Inc(I);
  until ((not (UntilGone)) or ((not (Click))));
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function LeveledUp: Boolean;
Contributors: Wanted
Description: Clicks to continue after level up, true if found.
Date Created: November 7th, 2011. By Wanted. RS2 Build 676.
Last Modified: November 7th, 2011. By Wanted. RS2 Build 676.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function LeveledUp: Boolean;
begin
  Result := LeveledUpEx(True, True);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function ClickContinueEx(Click, UntilGone: Boolean): Boolean;
Contributors: Wanted
Description: Clicks to continue, true if found.
Date Created: December 20th, 2011. By Wanted. RS2 Build 690.
Last Modified: December 20th, 2011. By Wanted. RS2 Build 690.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function ClickContinueEx(Click, UntilGone: Boolean): Boolean;
var
  C, T, I: Integer;
  P: TPoint;
begin
  Result := False;
  I := 0;
  repeat
    if (not (GetColor(295, 468) = 13750738)) then
      Break;
    Result := True;
    if (not (Click)) then
      Break;
    C := CountColor(clBlack, MCX1, MCY1, MCX2, MCY2);
    if (I = 0) then
      MouseBox(223, 460, 294, 474, ClickLeft)
    else
      Mouse(P.X, P.Y, 0, 0, True);
    GetMousePos(P.X, P.Y);
    T := GetSystemTime + RR(10000, 15000);
    while ((CountColor(clBlack, MCX1, MCY1, MCX2, MCY2) = C) and (GetSystemTime < T)) do
      WaitRR(200, 400);
    Inc(I);
  until (not (UntilGone));
  LeveledUp;
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function ClickContinue: Boolean;
Contributors: Wanted
Description: Clicks to continue, true if found.
Date Created: December 20th, 2011. By Wanted. RS2 Build 690.
Last Modified: December 20th, 2011. By Wanted. RS2 Build 690.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function ClickContinue: Boolean;
begin
  Result := ClickContinueEx(True, True);
end;