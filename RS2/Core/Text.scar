{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                              Official SCAR Include
                                   Runescape 2
                                  Text Routines
--------------------------------------------------------------------------------
 * function GetUpText: string;
     By: Freddy1990
 * function IsUpText(Text: string): Boolean;
     By: Freddy1990 & Wanted.
 * function IsUpTextMulti(Text: TStringArray): Boolean;
     By: Freddy1990 & Wanted.
 * function OptionBoxExistsEx(var OptionBox: TBox): Boolean;
     By: Wanted
 * function OptionBoxExists: Boolean;
     By: Wanted
 * function GetOptionsEx(var B: TBox; FilterBadChars: Boolean): TStringArray;
     By: Wanted
 * function GetOptions: TStringArray;
     By: Wanted
 * function OptionExistsMulti(Options: TStringArray; var InBox: TBoxArray): TBooleanArray;
     By: Wanted
 * function OptionExists(Option: string): Boolean;
     By: Wanted
 * function ChooseOptionMultiEx(Options: TStringArray; TypeC: ClickActions): Boolean;
     By: Wanted
 * function ChooseOptionMulti(Options: TStringArray): Boolean;
     By: Wanted
 * function ChooseOption(Option: string): Boolean;
     By: Wanted
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function GetUpText: String;
Contributors: Freddy1990, Wanted.
Description: Returns text in upperleft corner.
Date Created: August 7th, 2011. By Freddy1990. RS2 Build 660.
Last Modified: November 22nd, 2011. By Wanted. RS2 Build 682.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function GetUpText: string;
begin
  Result := rs_GetTextAt(UpChars, 8, 10);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function IsUpText(Text: string): Boolean;
Contributors: Freddy1990, Wanted.
Description: Returns true if uptext is found.
Date Created: August 7th, 2011. By Freddy1990 & Wanted. RS2 Build 660.
Last Modified: November 14th, 2011. By Wanted. RS2 Build 679.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function IsUpText(Text: string): Boolean;
var
  T: LongInt;
begin
  Result := True;
  T := GetSystemTime + RR(1500, 2500);
  while (GetSystemTime < T) do
    if (Pos(Text, GetUpText) > 0) then
      Exit
    else
      WaitRR(100, 250);
  Result := False;
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function IsUpTextMulti(Text: TStringArray): Boolean;
Contributors: Freddy1990, Wanted.
Description: Checks for the occurance of any of the strings in the array.
Date Created: August 7th, 2011. By Freddy1990 & Wanted. RS2 Build 660.
Last Modified: November 21st, 2011. By Wanted. RS2 Build 681.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function IsUpTextMulti(Text: TStringArray): Boolean;
var
  I, H, T: Integer;
  TheText: string;
begin
  Result := False;
  T := GetSystemTime + RR(1500, 2500);
  repeat
    TheText := GetUpText;
    H := High(Text);
    for I := 0 to H do
      if (Pos(Text[I], TheText) > 0) then
      begin
        Result := True;
        Exit;
      end;
    WaitRR(100, 250);
  until (GetSystemTime > T);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function OptionBoxExistsEx(var OptionBox: TBox): Boolean;
Contributors: Wanted
Description: Returns true if the box that contains the ChooseOptions is up.
OptionBoxTPA contains all the points within that match clOptionsBox.
Date Created: October 20th, 2011. By Wanted. RS2 Build 668.
Last Modified: November 24th, 2011. By Wanted. RS2 Build 682.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function OptionBoxExistsEx(var OptionBox: TBox): Boolean;
var
  TPA, TPA1, TPA2: TPointArray;
  ATPA: T2DPointArray;
begin
  Result := WaitFindColorsTolerance(TPA1, clOptionsBox, RSX1, RSY1, RSX2, RSY2, 0, 80, True, 50, 150, 1500, 2500);
  FindColors(TPA2, clOptionsBox2, RSX1, RSY1, RSX2, RSY2);
  TPA := CombineTPA(TPA1, TPA2);
  ATPA := TPAChain(TPA, 3);
  if (Length(ATPA) < 1) then
    Exit;
  SortATPASize(ATPA, True);
  OptionBox := GetTPABounds(ATPA[0]);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function OptionBoxExists: Boolean;
Contributors: Wanted
Description: Simpler version of OptionBoxExistsEx.
Date Created: October 20th, 2011. By Wanted. RS2 Build 668.
Last Modified: November 22nd, 2011. By Wanted. RS2 Build 681.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function OptionBoxExists: Boolean;
var
  TB: TBox;
begin
  Result := OptionBoxExistsEx(TB);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function GetOptionsEx(var B: TBox; FilterBadChars: Boolean): TStringArray;
Contributors: Wanted
Description: Returns options.
Date Created: December 1st, 2011. By Wanted. RS2 Build 687.
Last Modified: December 12th, 2011. By Wanted. RS2 Build 688.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function GetOptionsEx(var B: TBox; FilterBadChars: Boolean): TStringArray;
var
  H, I: Integer;
begin
  if (not (OptionBoxExistsEx(B))) then
    Exit;
  H := (Round((B.Y2 - B.Y1) / 16.25) - 1);
  SetLength(Result, H + 1);
  for I := 0 to H do
  begin
    Result[I] := rs_GetTextAt(OptionChars, B.X1 + 1, B.Y1 + 2 + (I * 16));
    if (FilterBadChars) then
    begin
      Result[I] := Replace(Result[I], ' ', '');
      Result[I] := Replace(Result[I], 'i', '');
      Result[I] := Replace(Result[I], 'y', '');
      Result[I] := Replace(Result[I], 'A', '');
    end;
  end;
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function GetOptions: TStringArray;
Contributors: Wanted
Description: Returns options.
Date Created: December 1st, 2011. By Wanted. RS2 Build 687.
Last Modified: December 1st, 2011. By Wanted. RS2 Build 687.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function GetOptions: TStringArray;
var
  B: TBox;
begin
  Result := GetOptionsEx(B, False);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function OptionExistsMulti(Options: TStringArray; var InBox: TBoxArray): TBooleanArray;
Contributors: Wanted
Description: Checks if options exist within the options box.
Date Created: October 20th, 2011. By Wanted. RS2 Build 668.
Last Modified: December 12th, 2011. By Wanted. RS2 Build 688.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function OptionExistsMulti(Options: TStringArray; var InBox: TBoxArray): TBooleanArray;
var
  Lines, NewOptions: TStringArray;
  I, II, H, HH: LongInt;
  B: TBox;
begin
  Lines := GetOptionsEx(B, False);
  H := High(Lines);
  if ((H + 1) < 1) then
    Exit;
  HH := High(Options);
  SetLength(Result, HH + 1);
  SetLength(InBox, HH + 1);
  for I := 0 to H do
    for II := 0 to HH do
      if (not (Result[II])) then
        if (Pos(Options[II], Lines[I]) > 0) then
        begin
          Result[II] := True;
          InBox[II] := Box(B.X1 + 2, B.Y1 + 2 + (I * 16), B.X2 - 2, B.Y1 + 17 + (I * 16));
          Break;
        end;
  for I := 0 to HH do
    if (Result[I]) then
      Exit;
  for I := 0 to H do
  begin
    Lines[I] := Replace(Lines[I], ' ', '');
    Lines[I] := Replace(Lines[I], 'i', '');
    Lines[I] := Replace(Lines[I], 'y', '');
    Lines[I] := Replace(Lines[I], 'A', '');
  end;
  NewOptions := Options;
  for I := 0 to HH do
  begin
    NewOptions[I] := Replace(Options[I], ' ', '');
    NewOptions[I] := Replace(Options[I], 'i', '');
    NewOptions[I] := Replace(Options[I], 'y', '');
    NewOptions[I] := Replace(Options[I], 'A', '');
  end;
  for I := 0 to H do
    for II := 0 to HH do
      if (not (Result[II])) then
        if (Pos(NewOptions[II], Lines[I]) > 0) then
        begin
          Result[II] := True;
          InBox[II] := Box(B.X1 + 2, B.Y1 + 2 + (I * 16), B.X2 - 2, B.Y1 + 17 + (I * 16));
          Break;
        end;
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function OptionExists(Option: string): Boolean;
Contributors: Wanted
Description: Checks if option exists within the options box.
Date Created: October 20th, 2011. By Wanted. RS2 Build 668.
Last Modified: December 12th, 2011. By Wanted. RS2 Build 688.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function OptionExists(Option: string): Boolean;
var
  TBoA: TBooleanArray;
  TBA: TBoxArray;
begin
  TBoA := OptionExistsMulti([Option], TBA);
  Result := (CheckTBoA(TBoA) > 0);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function ChooseOptionMultiEx(Options: TStringArray; TypeC: ClickActions): Boolean;
Contributors: Wanted
Description: Finds the first option in Option that exists and clicks it.
Date Created: October 20th, 2011. By Wanted. RS2 Build 668.
Last Modified: November 29th, 2011. By Wanted. RS2 Build 687.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function ChooseOptionMultiEx(Options: TStringArray; TypeC: ClickActions): Boolean;
var
  TBoA: TBooleanArray;
  TPA: TPointArray;
  TBA: TBoxArray;
  I, H: LongInt;
  P: TPoint;
  B: TBox;
begin
  TBoA := OptionExistsMulti(Options, TBA);
  H := High(TBoA);
  for I := 0 to H do
    if (TBoA[I]) then
    begin
      MouseBox(TBA[I].X1, TBA[I].Y1, TBA[I].X2, TBA[I].Y2, TypeC);
      Result := True;
      Exit;
    end;
  OptionBoxExistsEx(B);
  if ((B.X1 - 30) < RSX1) then
  begin
    B := Box(B.X1, B.Y1, B.X2 + 20, B.Y2);
    TPA := TPAFromBox(Box(B.X1, B.Y1, B.X2 + 30, B.Y2));
  end
  else
  begin
    B := Box(B.X1 - 20, B.Y1, B.X2, B.Y2);
    TPA := TPAFromBox(Box(B.X1 - 30, B.Y1, B.X2, B.Y2));
  end;
  TPAFilterBoxes(TPA, [B]);
  P := TPA[Random(Length(TPA))];
  MMouse(P.X, P.Y, 0, 0);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function ChooseOptionMulti(Options: TStringArray): Boolean;
Contributors: Wanted
Description: Finds the first option in Option that exists and clicks it.
Date Created: October 20th, 2011. By Wanted. RS2 Build 668.
Last Modified: November 29th, 2011. By Wanted. RS2 Build 687.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function ChooseOptionMulti(Options: TStringArray): Boolean;
begin
  Result := ChooseOptionMultiEx(Options, ClickLeft);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function ChooseOption(Option: string): Boolean;
Contributors: Wanted
Description: If Option exists clicks it.
Date Created: October 20th, 2011. By Wanted. RS2 Build 668.
Last Modified: October 20th, 2011. By Wanted. RS2 Build 668.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function ChooseOption(Option: string): Boolean;
begin
  Result := ChooseOptionMulti([Option]);
end;

//GetNPCChatText
//FindNPCChatText