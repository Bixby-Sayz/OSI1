{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                              Official SCAR Include
                             SMART Debugging Routines
--------------------------------------------------------------------------------
 * procedure SmartDebugText(Text: string; Chars, Color, X, Y: Integer);
 * procedure SmartDebugATPABoundsEx(ATPA: T2DPointArray; ShowTPAs, ShowATPABorder, ShowTPABorders, ShowTPAIndexes: Boolean);
 * procedure SmartDebugATPABounds(ATPA: T2DPointArray);
 * procedure SmartDebugATPA(ATPA: T2DPointArray);
 * procedure SmartDebugTPA(TPA: TPointArray);
 * procedure SmartDebugTBA(TBA: TBoxArray);
 * procedure SmartDebugTB(TB: TBox);
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
procedure SmartDebugText(Text: string; Chars, Color, X, Y: Integer);
Contributors: Wanted
Description: Debugs a line of text to the smart debug canvas.
Date Created: January 22nd, 2012. By Wanted. RS2 Build 699.
Last Modified: January 22nd, 2012. By Wanted. RS2 Build 699.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

procedure SmartDebugText(Text: string; Chars, Color, X, Y: Integer);
var
  Bmp: TSCARBitmap;
  I, H: Integer;
  TPA: TPointArray;
begin
  Bmp := TSCARBitmap.Create('');
  try
    Bmp.SetSize(RSPW, RSPH);
    Bmp.Clear(0);    
    TPA := LoadTextTPA(Text, Chars);  
    H := High(TPA);
    for I := 0 to H do
      Bmp.Pixels[TPA[I].X, TPA[I].Y] := Color;  
    SmartDebugBitmapEx(SMART_Client, Bmp, X, Y);
  finally
    Bmp.Free;
  end;
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
procedure SmartDebugATPABoundsEx(ATPA: T2DPointArray; ShowTPAs, ShowATPABorder, ShowTPABorders, ShowTPAIndexes: Boolean);
Contributors: Wanted
Description: Debugs an ATPA to the smart debug canvas with tons of options.
Date Created: January 22nd, 2012. By Wanted. RS2 Build 699.
Last Modified: January 22nd, 2012. By Wanted. RS2 Build 699.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

procedure SmartDebugATPABoundsEx(ATPA: T2DPointArray; ShowTPAs, ShowATPABorder, ShowTPABorders, ShowTPAIndexes: Boolean);
var                  
  Bmp: TSCARBitmap;
  I, II, H, HH, Col: Integer;
  ATPA2: T2DPointArray;
  TPA: TPointArray;
  B: TBox;
begin      
  Bmp := TSCARBitmap.Create('');
  try
    Bmp.SetSize(RSPW, RSPH);
    Bmp.Clear(0);    
    H := High(ATPA);
    SetLength(ATPA2, H + 1);
    for I := 0 to H do
      ATPA2[I] := CopyTPA(ATPA[I]);
    if ((ShowTPAs) or (ShowTPABorders) or (ShowTPAIndexes)) then
    begin
      for I := 0 to H do
      begin
        Col := Random(clWhite + 1);
        HH := High(ATPA2[I]);
        if (ShowTPAs) then
          for II := 0 to HH do   
            Bmp.Pixels[ATPA2[I][II].X, ATPA2[I][II].Y] := Col;
        if ((ShowTPABorders) or (ShowTPAIndexes)) then
          Col := Random(clWhite + 1);
        if (ShowTPABorders) then
        begin
          TPA := TBOutlineToTPA(TPABounds(ATPA2[I]));
          HH := High(TPA);
          for II := 0 to HH do                       
            Bmp.Pixels[TPA[II].X, TPA[II].Y] := Col;
        end;
        if (ShowTPAIndexes) then
        begin
          TPA := LoadTextTPA(IntToStr(I), StatChars);
          B := TPABounds(ATPA2[I]);
          OffsetTPA(TPA, B.X1 + 2, B.Y1 + 2);
          HH := High(TPA);
          for II := 0 to HH do
            try     
              Bmp.Pixels[TPA[II].X, TPA[II].Y] := Col;
            except
            end;
        end;
      end;
      if (ShowATPABorder) then
      begin
        TPA := TBOutlineToTPA(ATPABounds(ATPA2));
        H := High(TPA);
        for I := 0 to H do   
          Bmp.Pixels[TPA[I].X, TPA[I].Y] := clBlue;
      end;
    end;
    SmartDebugBitmap(SMART_Client, Bmp);
  finally
    Bmp.Free;
  end;
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
procedure SmartDebugATPABounds(ATPA: T2DPointArray);
Contributors: Wanted
Description: Debugs an ATPA to the smart debug canvas with all options enabled.
Date Created: January 22nd, 2012. By Wanted. RS2 Build 699.
Last Modified: January 22nd, 2012. By Wanted. RS2 Build 699.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

procedure SmartDebugATPABounds(ATPA: T2DPointArray);
begin
  SmartDebugATPABoundsEx(ATPA, True, True, True, True);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
procedure SmartDebugATPA(ATPA: T2DPointArray);
Contributors: Wanted
Description: Debugs an ATPA to the smart debug canvas.
Date Created: January 22nd, 2012. By Wanted. RS2 Build 699.
Last Modified: January 22nd, 2012. By Wanted. RS2 Build 699.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

procedure SmartDebugATPA(ATPA: T2DPointArray);
begin
  SmartDebugATPABoundsEx(ATPA, True, True, False, False);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
procedure SmartDebugTPA(TPA: TPointArray);
Contributors: Wanted
Description: Debugs a TPA to the smart debug canvas.
Date Created: January 22nd, 2012. By Wanted. RS2 Build 699.
Last Modified: January 22nd, 2012. By Wanted. RS2 Build 699.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

procedure SmartDebugTPA(TPA: TPointArray);
begin
  SmartDebugATPA([TPA]);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
procedure SmartDebugTBA(TBA: TBoxArray);
Contributors: Wanted
Description: Debugs a TBoxArray to the smart debug canvas.
Date Created: January 22nd, 2012. By Wanted. RS2 Build 699.
Last Modified: January 22nd, 2012. By Wanted. RS2 Build 699.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

procedure SmartDebugTBA(TBA: TBoxArray);
var
  ATPA: T2DPointArray;
  I, H: LongInt;
begin
  H := High(TBA);
  SetLength(ATPA, H + 1);
  for I := 0 to H do
    ATPA[I] := [Point(TBA[I].X1, TBA[I].Y1), Point(TBA[I].X2, TBA[I].Y2)];
  SmartDebugATPABoundsEx(ATPA, False, False, True, True);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
procedure SmartDebugTB(TB: TBox);
Contributors: Wanted
Description: Debugs a TBox to the smart debug canvas.
Date Created: January 22nd, 2012. By Wanted. RS2 Build 699.
Last Modified: January 22nd, 2012. By Wanted. RS2 Build 699.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

procedure SmartDebugTB(TB: TBox);
begin
  SmartDebugTBA([TB]);
end;